plugins {
    id 'application'
    id("com.google.protobuf") version "0.9.3"
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'io.github.giuliapais.robotsnetwork.RobotLauncher'
}

dependencies {
    // https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-client
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '3.1.2'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.media/jersey-media-json-jackson
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '3.1.2'
    // https://mvnrepository.com/artifact/org.glassfish.jersey.inject/jersey-hk2
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '3.1.2'
    implementation 'info.picocli:picocli:4.7.4'
    // https://mvnrepository.com/artifact/commons-validator/commons-validator
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    // https://mvnrepository.com/artifact/com.diogonunes/JColor
    implementation group: 'com.diogonunes', name: 'JColor', version: '5.5.1'
    // https://mvnrepository.com/artifact/org.eclipse.paho/org.eclipse.paho.client.mqttv3
    implementation group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'
    implementation project(path: ':commons')

    // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: '1.56.1'
    // https://mvnrepository.com/artifact/io.grpc/grpc-stub
    implementation group: 'io.grpc', name: 'grpc-stub', version: '1.56.1'
    // https://mvnrepository.com/artifact/io.grpc/grpc-netty
    implementation group: 'io.grpc', name: 'grpc-netty', version: '1.56.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'


    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0-M1'
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.4.0'
    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.4.0'
}

run {
    standardInput = System.in
    if (project.hasProperty("runArgs")) {
        args project.property("runArgs").split()
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.56.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

test {
    useJUnitPlatform()
}